# Maintainer: Ovi Chis <ovi@ovios.org>
pkgname=tftp-hpa
pkgver=5.2
pkgrel=9
pkgdesc="Official tftp server"
arch=('x86_64')
url="http://www.kernel.org/pub/software/network/tftp/tftp-hpa/"
license=('BSD')
backup=('etc/tftpd.conf')
depends=('readline>=6.0.00')
source=($pkgname-$pkgver.tar.gz
       LICENSE
       tftpd.conf
       tftp-hpa-0.49-fortify-strcpy-crash.patch)
groups=('ovios')
prepare() {
  cd ${pkgname}-${pkgver}
  # fix #28103
  patch -Np1 -i ../tftp-hpa-0.49-fortify-strcpy-crash.patch
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr --mandir=/usr/share/man --sbindir=/usr/sbin --without-tcpwrappers
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALLROOT="${pkgdir}" install
  install -D -m644 "${srcdir}/tftpd.conf" "$pkgdir/etc/tftpd.conf"
  install -d "${pkgdir}/srv/tftp"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Remove conflict with iputils
  rm "${pkgdir}/usr/share/man/man8/tftpd.8"
}
md5sums=('3de3038e7c2bf6fc5d496825893ac8e7'
         '6ce21e27b6fdc1a1adf85c81e42aeecf'
         'b372088e550683df5ea8528ca6bbed1f'
         '22e8629ef19bc276a102c5d4d284c1bd')
